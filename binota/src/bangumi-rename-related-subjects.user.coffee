`// ==UserScript==
// @name        Bangumi-Rename-Related-Subjects
// @namespace   BRRS
// @description Quickly rename all related subjects at the same time.
// @include     /^https?:\/\/((bgm|bangumi)\.tv|chii\.in)\/subject\/\d+\/add_related\/subject/
// @version     1.0.0
// @grant       none
// ==/UserScript==
// Generated by CoffeScript ;) `

'use strict'

#############################################################
# --------------------------------------------------------- #
# -- Basic Varitables ------------------------------------- #
# --------------------------------------------------------- #
#############################################################

platforms =
  book: [{id: 1001, name: '漫画', meta: 'Manga'},
         {id: 1002, name: '小说', meta: 'Novel'},
         {id: 1003, name: '画集', meta: 'Book'},
         {id: 0, name: '其它', meta: 'Book'}],
  anime: [{id: 1, name: 'TV', meta: 'TVAnime'},
          {id: 2, name: 'OVA', meta: 'OVA'},
          {id: 3, name: '剧场版', meta: 'Movie'},
          {id: 0, name: '其它', meta: 'Anime'}],
  music: [],
  game: [],
  real: [{id: 1, name: '日剧', meta: 'Television'},
         {id: 2, name: '欧美剧', meta: 'Television'},
         {id: 3, name: '华语剧', meta: 'Television'},
         {id: 0, name: '其它', meta: 'Television'}]

formhash = $('input[name="formhash"]').val()
platform = window.location.href.match(/add_related\/subject\/(anime|book|music|game|real)/)[1]

#############################################################
# --------------------------------------------------------- #
# -- Templates -------------------------------------------- #
# --------------------------------------------------------- #
#############################################################

templates = {}

templates.platforms = do ->
  platformContent = ''
  tpl = '<label for="platform-{{bgmid}}-{{id}}"><input id="platform-{{bgmid}}-{{id}}" type="radio" value="{{id}}" name="platform-{{bgmid}}"> {{name}}</label>'
  for i in platforms[platform]
    platformContent += tpl.replace(/{{meta}}/g, i.meta).replace(/{{id}}/g, i.id).replace(/{{name}}/g, i.name)
  platformContent

templates.css = """
  /*-----------------------------------------------------*\
  |*-- Global  ------------------------------------------*|
  \*-----------------------------------------------------*/
  #brrs-workspace .hidden {
    display: none; }

  #brrs-workspace .chiiBtn.large {
    margin: 5px 2px;
    padding: 5px 10px; }

  /*-----------------------------------------------------*\
  |*-- Launcher & Loading -------------------------------*|
  \*-----------------------------------------------------*/
  #brrs-workspace-launcher,
  #brrs-workspace-loading {
    width: 250px;
    margin: 170px auto;
    font-size: 18px;
    text-align: center; }
    #brrs-workspace-loading .brrs-progress {
      margin-top: 3px;
      height: 2px;
      border: none;
      background: #f09199; }
  
  /*-----------------------------------------------------*\
  |*-- Toolkit ------------------------------------------*|
  \*-----------------------------------------------------*/
  #brrs-toolkit {
    width: 100%; }
    #brrs-toolkit .brrs-col-main {
      margin-left: 5%; }

  /*-----------------------------------------------------*\
  |*-- Editor -------------------------------------------*|
  \*-----------------------------------------------------*/
  #brrs-subjects-editor {
    width: 100%; }
    #brrs-subjects-editor .renameSubject {
      width: calc(100% - 20px);
      height: 15px; }
    #brrs-subjects-editor .renameSubject {
      width: 40px;
      height: 15px; }
    #brrs-subjects-editor .item {
      height: 40px;
      background: #F9F9F9;
      border-bottom: 1px solid #E0E0E0;
      clear: both;
      padding: 5px 10px; }
  .brrs-editor-item {
    padding: 6px 0 3px 0;
    border-left: 2px solid #369cf8;
    border-bottom: 1px solid #D9D9D9; }
    .brrs-editor-item:hover {
      background: #F4F4F4; }
    [class^="brrs-col-"] {
      line-height: 24px;
      vertical-align: middle;
      overflow: hidden;
      float: left; }
    .brrs-col-close {
      text-align: center;
      font-size: 18px;
      width: 2%; }
    .brrs-col-id {
      text-align: center;
      padding-right: 3px;
      width: calc(3% - 3px); }
    .brrs-col-main {
      width: 60%; }
    .brrs-col-platform {
      width: 23%; }
    .brrs-col-actions {
      width: 12%; }

    .brrs-editor-item .details {
      margin-left: 5%; }

    #brrs-workspace-editor input,
    #brrs-workspace-editor select {
      vertical-align: middle; }
    #brrs-workspace-editor input.title {
      width: calc(100% - 20px); }
    #brrs-workspace-editor label {
      padding: 0 6px; }

  /*-----------------------------------------------------*\
  |*-- Previewer ----------------------------------------*|
  \*-----------------------------------------------------*/

  /*-----------------------------------------------------*\
  |*-- Footer -------------------------------------------*|
  \*-----------------------------------------------------*/
  #brrs-footer {
    text-align: right;
    color: #999;
    font-size: 12; }
"""

templates.workspace = do ->
  platformContent = templates.platforms.replace(/{{bgmid}}/g, 'master')

  tpl = """
    <style>
      #{templates.css}
    </style>
    <div id="brrs-workspace" class="columns clearit" style="padding: 10px 0;">
      <div id="brrs-workspace-loading" class="hidden clearit">
        <p>少女祈祷中……(<span class="brrs-var-loaded"></span>/<span class="brrs-var-total"></span>)</p>
        <div class="brrs-progress"></div>
      </div>
      <div id="brrs-workspace-editor" class="hidden clearit">
        <div id="brrs-toolkit">
          <div class="brrs-col-main">
            <a class="chiiBtn" href="#brrs-tool-restore" data-action="restore">还原所有操作</a>/
            <a class="chiiBtn" href="#brrs-tool-removechs" data-action="removechs">去中文名</a>
            <!-- a class="chiiBtn" href="#brrs-tool-removepattern" data-action="removepattern">批量删除标题指定字符</a -->
            <div id="brrs-toolkit-settitle">
              <label>
                标题：
                <input type="text" name="pattern" class="inputtext" style="width: 40%" value="{{SeriesTitle}} ({{SeriesNumber}})">
              </label>
              <label>
                序号起始：
                <input type="text" name="seriesstart" class="inputtext" style="width: 20px;" value="1">
              </label>
              <a class="chiiBtn" href="#brrs-tool-settitle" data-action="settitle">批量替换标题</a>
              <p>{{SeriesTitle}} = 系列标题；{{SeriesNumber}} = 序号</p>
            </div>
            <div id="brrs-toolkit-removepattern" class="hidden">
              <h3>批量删除标题指定字符</h3>
            </div>
          </div>
    
          <div class="brrs-col-platform">
            #{platformContent}
          </div>
          <div class="fixit clearit"></div>
        </div>
        <div id="brrs-subjects-editor" class="clearit">
        </div>
        <a class="chiiBtn large" href="#brrs-previewer">编辑完成，点击预览</a>
      </div>
      <div id="brrs-workspace-previewer" class="hidden clearit">
      </div>
      <div id="brrs-footer" class="clearit">
        <p>Powered by BRRS.</p>
        <p>脚本问题反馈/功能请求：<a href="/group/topic/311647" target="_blank">小组讨论帖</a> /
                                  <a href="https://github.com/bangumi/scripts/issues" target="_blank">GitHub Issues</a>.</p>
      </div>
    </div>
"""

templates.editor = do ->
  tpl = """
  <div class="brrs-editor-item clearit" data-id="{{id}}" data-bgmid="{{bgmid}}">
    <div class="brrs-col-close"><a class="brrs-remove" href="#brrs-removeitem" data-action="removeitem" data-id="{{id}}">&times;</a></div>
    <div class="brrs-col-id">{{id}}</div>
    <div class="brrs-col-main"><input type="text" class="inputtext title" value="{{title}}" placeholder="{{title}}" name="title"></div>
    <div class="brrs-col-platform">#{templates.platforms}</div>
    <div class="brrs-col-actions"><a class="chiiBtn" href="#brrs-editor-details" data-action="editdetails" data-id="{{id}}">编辑详细信息</a></div>
    <div class="details clearit hidden">
      <textarea class="quick" name="infobox" rows="15" style="width: 48%"></textarea>
      <textarea class="quick" name="summary" rows="15" style="width: 48%"></textarea>
      <p><a class="chiiBtn large" href="#brrs" data-action="closedetails" data-id="{{id}}">收起详细信息</a></p>
    </div>
    <div class="fixit clearit"></div>
  </div>
"""

#############################################################
# --------------------------------------------------------- #
# -- Interface Function ----------------------------------- #
# --------------------------------------------------------- #
#############################################################

BrrsView =
  workspace: (name) ->
    $('[id^=brrs-workspace-]').hide()
    $("#brrs-workspace-#{name}").show()
  loading:
    setTotal: (total) ->
      $('.brrs-var-total').html(total)
    setProgress: (loaded) ->
      $('.brrs-var-loaded').html(loaded)
      prg = (parseInt($('.brrs-var-loaded').text())) / (parseInt($('.brrs-var-total').text()))
      $('#brrs-workspace-loading .brrs-progress').animate({width: (prg * 100) + '%'}, 50)
    getTotal: ->
      parseInt($('.brrs-var-total').text())
    getLoaded: ->
      parseInt($('.brrs-var-loaded').text())

  toolkit:
    restore: ->
      $('#brrs-subjects-editor input[type="text"], #brrs-subjects-editor textarea').each ->
        $(this).val $(this).attr 'placeholder'
      $('.brrs-editor-item .brrs-col-platform').each ->
        $(this).find("[value=\"#{$(this).attr('data-origin')}\"]").attr 'checked', true
    removechs: ->
      $('#brrs-subjects-editor textarea[name="infobox"]').each ->
        return unless $(this).val().match(/中文名=(.+)/)?[1].trim().length > 0
        $(this).val $(this).val().replace /中文名=(.+)/, '中文名= '
    settitle: ->
      pattern = $('#brrs-toolkit-settitle input[name="pattern"]').val()
      start = parseInt $('#brrs-toolkit-settitle input[name="seriesstart"]').val()
      if pattern.match(/{{SeriesTitle}}/)?
        pattern = pattern.replace /{{SeriesTitle}}/g, $('.nameSingle a').text()
      $('#brrs-subjects-editor input.title').each ->
        $(this).val pattern.replace /{{SeriesNumber}}/g, start++
      
  editor:
    insert: (id, data) ->
      tpl = templates.editor
      tpl = tpl.replace /{{id}}/g, id
      tpl = tpl.replace(/{{bgmid}}/g, data.bgmid) if data.bgmid?
      tpl = tpl.replace(/{{title}}/g, data.title) if data.title?

      $('#brrs-subjects-editor').append(tpl);
    remove: (id) ->
      $(".brrs-editor-item[data-id=\"#{id}\"]").remove()
    details:
      show: (id) ->
        selector = ".brrs-editor-item[data-id=\"#{id}\"] .details"
        $(".brrs-editor-item[data-id=\"#{id}\"] a[data-action=\"editdetails\"]").fadeOut 700 
        $(selector).css 'height', 0
        $(selector).show()
        $(selector).animate {'height': '362px'}, 700, ->
          $(this).css 'min-height', '362px'
          $(this).css 'height', 'auto'
      hide: (id) ->
        selector = ".brrs-editor-item[data-id=\"#{id}\"] .details"
        $(selector).css 'height', '362px'
        $(selector).css 'min-height', 0
        $(selector).animate {'height': 0}, 700, ->
          $(selector).hide()
        $(".brrs-editor-item[data-id=\"#{id}\"] a[data-action=\"editdetails\"]").fadeIn 700
    setPlatform: (bgmid, platform) ->
      selector = "input[name=\"platform-#{bgmid}\"][value=\"#{platform}\"]"
      origin = $(selector).parent().attr 'data-origin'
      $(selector).attr 'checked', 'true' 
      $(selector).parent().parent().attr('data-origin', platform) unless origin?
    setInput: (bgmid, field, text) ->
      selector = ".brrs-editor-item[data-bgmid=\"#{bgmid}\"] textarea[name=\"#{field}\"]"
      $(selector).val(text)
      $(selector).attr('placeholder', text) unless $(selector).attr('placeholder')

Wcode =
  parse: (wcode) ->
    retval = {}
    wcode = wcode.replace /\r/, ''
    json = wcode.replace /{{Infobox\s+\w+\/\w+\n([\S\s]*)}}/m, '{\n$1}'
    json = json.replace /\|.+?=\s*\n/g, ''                               #|AAA=\n => remove
    json = json.replace /\|(.+?)=\s*{\n*\s*}/, ''                        #|AAA={} => remove
    json = json.replace /\|(.+?)=\s*([^{}]+?)\n/g, '  "$1": "$2",\n'     #|AAA=BBB\n => 'AAA': "BBB",\n
    json = json.replace /\[(.+?\||)(.+?)\]\n/g, '"$2", '                 #[A|B] or [B] => "B",
    json = json.replace /\|(.+?)=\s*{\n/, '  "$1": ['                    #|AAA={ => 'AAA': [
    json = json.replace /,\s*}\n/g, ' ],\n'                              #, }\n => ]\n
    json = json.replace /,\s*}/g, '}'                                    #, } => }
    JSON.parse json

#############################################################
# --------------------------------------------------------- #
# -- Bootstrap -------------------------------------------- #
# --------------------------------------------------------- #
#############################################################

$(templates.workspace).insertBefore('.mainWrapper .columns');
$('<a class="chiiBtn rr" id="brrs-launcher-normal" href="#">BRRS</a>').insertAfter('#modifyOrder')
$('<a class="chiiBtn rr" id="brrs-launcher-offprint" href="#">BRRS 单行本</a>').insertAfter('#modifyOrder')

$('#brrs-launcher-normal').click ->
  subjects = do ->
    retval = []
    $('#crtRelateSubjects > li').each ->
      subject = {}
      subject.bgmid = $(this).attr('item_id')
      subject.title = $(this).find('.title a').text().trim()
      subject.relation = $(this).find('option[selected="true"]').val()
      retval.push(subject)
    retval
  BrrsView.loading.setTotal(subjects.length)
  BrrsView.loading.setProgress(0)
  BrrsView.workspace('loading')

  for value, key in subjects
    BrrsView.editor.insert(key, value)
    $.get "/subject/#{value.bgmid}/edit_detail", (data) ->
      BrrsView.loading.setProgress(BrrsView.loading.getLoaded() + 1)
      bgmid = data.match(/<a href="\/subject\/(\d+)" title="[^"]*" property="v:itemre/)[1];
      BrrsView.editor.setInput(bgmid, 'infobox', data.match(/subject_infobox"[^>]+>([\S\s]*?)<\/textarea>/m)[1])
      BrrsView.editor.setInput(bgmid, 'summary', data.match(/subject_summary"[^>]+>([\S\s]*?)<\/textarea>/m)[1])
      BrrsView.editor.setPlatform(bgmid, data.match(/value="(\d+)" onclick="WikiTpl\('[^']+'\)" checked>/)[1])
      if BrrsView.loading.getTotal() == BrrsView.loading.getLoaded()
        EventBinding()
        $('#brrs-workspace-loading').fadeOut 1000, ->
          BrrsView.workspace('editor')

  null

#############################################################
# --------------------------------------------------------- #
# -- Event Binding ---------------------------------------- #
# --------------------------------------------------------- #
#############################################################

$('#brrs-toolkit a[data-action="restore"]').click ->
  BrrsView.toolkit.restore()
$('#brrs-toolkit a[data-action="removechs"]').click ->
  BrrsView.toolkit.removechs()
$('#brrs-toolkit a[data-action="settitle"]').click ->
  BrrsView.toolkit.settitle()

$('#brrs-toolkit input[type="radio"]').click ->
  newPlatform = $(this).val()
  $("#brrs-subjects-editor input[value=\"#{$(this).val()}\"]").attr 'checked', true

EventBinding = ->
  $('a[data-action="removeitem"]').click ->
    BrrsView.editor.remove $(this).attr 'data-id'
  $('a[data-action="editdetails"]').click ->
    BrrsView.editor.details.show $(this).attr 'data-id'
  $('a[data-action="closedetails"]').click ->
    BrrsView.editor.details.hide $(this).attr 'data-id'

#############################################################
# --------------------------------------------------------- #
# -- Preview ---------------------------------------------- #
# --------------------------------------------------------- #
#############################################################


